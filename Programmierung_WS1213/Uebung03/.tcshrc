# @(#) $Header: 4.2 Standard ~/.tcshrc$
# November 1995 Dipl. Phys. Thomas Scheunemann    HRZ Uni -GH- Duisburg
# Diese Datei sollte vom Benutzer nicht geaendert werden, um ein spaeteres
# Ersetzen durch eine neuere Version zu Ermoeglichen. Wenn Sie Anpassungen
# vornehmen wollen, legen Sie eine Datei .mytcshrc an. Diese wird, wenn sie
# vorhanden ist im Anschluss an dieses Skript ausgefuehrt.

# Zur Referenz
if( ! $?RC_FILES ) then
        setenv RC_FILES .tcshrc
else
	setenv RC_FILES ${RC_FILES}:.tcshrc
endif

# Fuehre die Befehle nur in der ersten Shell aus. (Dies muss nicht unbedingt
# eine login-shell sein.)
if ( ! $?SET_ENV ) then

	# Finde den Systemnamen heraus
	if ( ! $?HOST && -f /bin/hostname ) then
		setenv HOST `/bin/hostname`
	endif

	# Finde heraus, auf welchem System wir laufen
	if ( ! $?HOSTTYPE && -f /etc/hosttype ) then
		setenv HOSTTYPE `/etc/hosttype`
	endif

	# Default HOST
	if ( ! $?HOST ) then
		setenv HOST unknown
	endif

	# Default HOSTTYPE
	if ( ! $?HOSTTYPE ) then
		setenv HOSTTYPE unknown
	endif

	# Default HOME
	if ( ! $?HOME ) then
		setenv HOME /
	endif

	# Setze Startwerte
	setenv DPATH ""
	setenv MANPATH ""
	setenv LIBPATH ""
	setenv LDOPTS ""

	# Zusaetzliche Pfade fuer ausgewaehlte Pakete
	set packets="/usr/local /usr/contrib"
	if ( -r $HOME/.packets ) then
		set packets="`cat $HOME/.packets` $packets"
	endif
	if ( -d /opt ) then
		set packets="$packets /opt/*"
	endif
	foreach packet ( $packets )
		if ( -d ${packet} ) then
			if ( -d ${packet}/bin ) then
				setenv DPATH ${DPATH}${packet}/bin:
			endif
			if ( -d ${packet}/scripts ) then
				setenv DPATH ${DPATH}${packet}/scripts:
			endif
			if ( -d ${packet}/man ) then
				setenv MANPATH ${MANPATH}${packet}/man:
			endif
			if ( -d ${packet}/share/man ) then
				setenv MANPATH ${MANPATH}${packet}/share/man:
			endif
			if ( -d ${packet}/lib ) then
				setenv LIBPATH ${LIBPATH}${packet}/lib:
				setenv LDOPTS "${LDOPTS}-L${packet}/lib "
			endif
		endif
	end
	unset packets

	# Setze Default Pfad
	setenv DPATH /usr/bin/X11:${DPATH}/usr/ucb:/bin:/sbin:/etc:/usr/bin:/usr/sbin:/usr/etc

	# Setze Pfad fuer man-pages
	setenv MANPATH ${MANPATH}/usr/share/man:/usr/man

	# Setze Pfad fuer Libraries
	setenv LIBPATH ${LIBPATH}/usr/lib:/lib
	setenv LDOPTS "${LDOPTS}-L/usr/lib -L/lib"

	# Setze den richtigen Pfad nur einmal
	setenv PATH ${DPATH}:${HOME}/bin:.

# Ende der Voreinstellungen
endif

# Setze den Kommandoprompt
set prompt="%n@%m:%~%# "

# Setze die Anzahl Befehle, die aufgehoben werden
set history=100
set savehist=50

# Setze einige Standard Aliase
alias ll 'ls -l'
alias la 'ls -al'
alias dir 'ls -l'
alias m 'memacs'

# Damit pwd den "richtigen" Pfad ausgibt:
alias pwd 'echo $cwd'

# Pfad fuer change directory
set cdpath=(. .. $HOME)

# Einige weitere Variablen speziell fuer die tcsh
set correct=cmd
set autolist
set symlinks=expand

# Setze File-Erzeugungs-Maske
umask 077

# Fuehre nun die Benutzereigene Datei .mytcshrc aus
if ( -r $HOME/.mytcshrc ) then
	source $HOME/.mytcshrc
endif

# .tcshrc wurde ausgefuehrt
setenv SET_ENV YES
